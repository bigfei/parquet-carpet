plugins {
    id 'java'
    id 'java-library'    
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.apache.parquet:parquet-common:${parquetVersion}"
    api "org.apache.parquet:parquet-column:${parquetVersion}"
    api "org.apache.parquet:parquet-hadoop:${parquetVersion}"
    compileOnly ("org.apache.hadoop:hadoop-client:${hadoopVersion}")
    implementation("org.apache.hadoop:hadoop-mapreduce-client-core:${hadoopVersion}"){
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'org.apache.hadoop', module: 'hadoop-yarn-client'
        exclude group: 'org.apache.hadoop', module: 'hadoop-yarn-common'
        exclude group: 'org.apache.hadoop', module: 'hadoop-hdfs-client'
        exclude group: 'com.google.inject.extensions', module: 'guice-servlet'
        exclude group: 'io.netty', module: 'netty'
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'javax.ws.rs', module: 'javax.ws.rs-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'slf4j-reload4j'          
    } 
    api("org.apache.hadoop:hadoop-common:${hadoopVersion}") {
        exclude group: 'org.apache.avro', module: 'avro'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'org.apache.kerby', module: 'kerb-core'
        exclude group: 'org.apache.curator', module: 'curator-recipes'
        exclude group: 'org.apache.curator', module: 'curator-client'
        exclude group: 'org.apache.hadoop', module: 'hadoop-annotations'
        exclude group: 'org.apache.hadoop.thirdparty', module: 'hadoop-shaded-protobuf_3_7'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'org.apache.commons', module: 'commons-text'
        exclude group: 'org.apache.commons', module: 'commons-math3'
        exclude group: 'org.apache.zookeeper', module: 'zookeeper'
        exclude group: 'org.eclipse.jetty', module: 'jetty-server'
        exclude group: 'org.eclipse.jetty', module: 'jetty-servlet'
        exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
        exclude group: 'org.eclipse.jetty', module: 'jetty-util'
        exclude group: 'javax.servlet', module: 'javax.servlet-api'
        exclude group: 'javax.servlet.jsp', module: 'jsp-api'
        exclude group: 'com.sun.jersey', module: 'jersey-json'
        exclude group: 'com.sun.jersey', module: 'jersey-core'
        exclude group: 'com.sun.jersey', module: 'jersey-servlet'
        exclude group: 'com.sun.jersey', module: 'jersey-server'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
        exclude group: 'commons-codec', module: 'commons-codec'
        exclude group: 'commons-io', module: 'commons-io'
        exclude group: 'commons-net', module: 'commons-net'
        exclude group: 'commons-cli', module: 'commons-cli'
        exclude group: 'commons-pool', module: 'commons-pool'
        exclude group: 'ch.qos.reload4j', module: 'reload4j'
        exclude group: 'org.slf4j', module: 'slf4j-reload4j'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'    
        exclude group: 'com.jcraft', module: 'jsch'
        exclude group: 'dnsjava', module: 'dnsjava'        

        exclude group: 'org.apache.kerby', module: 'kerb-simplekdc'
        exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
        exclude group: 'net.minidev', module: 'json-smart'
        exclude group: 'org.apache.curator', module: 'curator-framework'
    }

    testImplementation project(':carpet-filestream')
    testImplementation "org.apache.avro:avro:1.10.2"
    
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation "org.apache.parquet:parquet-avro:${parquetVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

configurations.implementation {
    //hadoop-mapreduce-client-core
    exclude group: 'org.apache.hadoop', module: 'hadoop-yarn-client'
    exclude group: 'org.apache.hadoop', module: 'hadoop-yarn-common'
    exclude group: 'org.apache.hadoop', module: 'hadoop-hdfs-client'
    exclude group: 'com.google.inject.extensions', module: 'guice-servlet'
    exclude group: 'io.netty', module: 'netty'
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
    exclude group: 'javax.ws.rs', module: 'javax.ws.rs-api'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'org.slf4j', module: 'slf4j-reload4j'  

    //hadoop-common
    //Following dependencies are needed for Path
    //exclude group: 'org.apache.commons', module: 'commons-lang3'
    //exclude group: 'commons-logging', module: 'commons-logging'
    //exclude group: 'org.apache.commons', module: 'commons-configuration2'
    //exclude group: 'org.apache.hadoop', module: 'hadoop-auth'
    exclude group: 'org.apache.kerby', module: 'kerb-core'
    exclude group: 'org.apache.curator', module: 'curator-recipes'
    exclude group: 'org.apache.curator', module: 'curator-client'
    exclude group: 'org.apache.hadoop', module: 'hadoop-annotations'
    exclude group: 'org.apache.hadoop.thirdparty', module: 'hadoop-shaded-protobuf_3_7'
    exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    exclude group: 'org.apache.commons', module: 'commons-text'
    exclude group: 'org.apache.commons', module: 'commons-math3'
    exclude group: 'org.apache.zookeeper', module: 'zookeeper'
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
    exclude group: 'org.eclipse.jetty', module: 'jetty-servlet'
    exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
    exclude group: 'org.eclipse.jetty', module: 'jetty-util'
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
    exclude group: 'javax.servlet.jsp', module: 'jsp-api'
    exclude group: 'com.sun.jersey', module: 'jersey-json'
    exclude group: 'com.sun.jersey', module: 'jersey-core'
    exclude group: 'com.sun.jersey', module: 'jersey-servlet'
    exclude group: 'com.sun.jersey', module: 'jersey-server'
    exclude group: 'com.google.guava', module: 'guava'
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
    exclude group: 'commons-beanutils', module: 'commons-beanutils'
    exclude group: 'commons-codec', module: 'commons-codec'
    exclude group: 'commons-io', module: 'commons-io'
    exclude group: 'commons-net', module: 'commons-net'
    exclude group: 'commons-cli', module: 'commons-cli'
    exclude group: 'commons-pool', module: 'commons-pool'
    exclude group: 'ch.qos.reload4j', module: 'reload4j'
    exclude group: 'org.slf4j', module: 'slf4j-reload4j'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'log4j', module: 'log4j'    
    exclude group: 'com.jcraft', module: 'jsch'
    exclude group: 'dnsjava', module: 'dnsjava'

    //Following dependencies excluded from included hadoop-auth
    exclude group: 'org.apache.kerby', module: 'kerb-simplekdc'
    exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
    exclude group: 'net.minidev', module: 'json-smart'
    exclude group: 'org.apache.curator', module: 'curator-framework'
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("org.apache.avro:avro:1.7.7") using module("org.apache.avro:avro:1.10.2")
    }        
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
         html {
            enabled true
         }
         xml {
            enabled true
         }
     }
}

task sourceJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.jerolba'
            artifactId 'carpet-record'
            version version

            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
    if (project.hasProperty('sonatypeUsername')){
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }    
    } else {
        println 'No sonatype configuration found. Expected user and password at $HOME/.gradle/gradle.properties file.'        
    }    
}

signing {
    sign publishing.publications.mavenJava
}


def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Carpet Record serialize/deserialize Parquet files using Java Records'
            name 'Carpet Record'
            url 'https://github.com/jerolba/parquet-carpet'
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/jerolba/parquet-carpet'
                connection 'scm:git:git://github.com/jerolba/parquet-carpet'
                developerConnection 'scm:git:ssh://github.com:jerolba/parquet-carpet'
            }
            developers {
                developer {
                    id 'jerolba'
                    name 'Jerónimo López'
                }
            }
        }
    }
}
