plugins {
    id 'java'
    id 'java-library'    
    id 'jacoco'
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.apache.parquet:parquet-common:${parquetVersion}"
}


test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.jerolba'
            artifactId 'carpet-filestream'
            version version

            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
    if (project.hasProperty('sonatypeUsername')){
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }    
    } else {
        println 'No sonatype configuration found. Expected user and password at $HOME/.gradle/gradle.properties file.'        
    }    
}

signing {
    sign publishing.publications.mavenJava
}


def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Carpet FileStream implements parquet InputFile and OuutputFile abtractions for local file system'
            name 'Carpet FileStreams'
            url 'https://github.com/jerolba/parquet-carpet'
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/jerolba/parquet-carpet'
                connection 'scm:git:git://github.com/jerolba/parquet-carpet'
                developerConnection 'scm:git:ssh://github.com:jerolba/parquet-carpet'
            }
            developers {
                developer {
                    id 'jerolba'
                    name 'Jerónimo López'
                }
            }
        }
    }
}
